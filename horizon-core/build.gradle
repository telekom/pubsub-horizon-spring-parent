// Copyright 2024 Deutsche Telekom IT GmbH
//
// SPDX-License-Identifier: Apache-2.0

import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java-library'
    id 'org.springframework.boot' apply false
    id 'io.spring.dependency-management'
    id 'maven-publish'
    id 'jacoco'
    id 'signing'
}

group 'de.telekom.eni'

ext {
    isReleaseVersion = !version.endsWith("SNAPSHOT")
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    withJavadocJar()
    withSourcesJar()
}

dependencyManagement {
    imports {
        mavenBom SpringBootPlugin.BOM_COORDINATES
    }
}

dependencies {
    implementation "com.fasterxml.jackson.core:jackson-databind"
    implementation "jakarta.validation:jakarta.validation-api"

    // Must be compileOnly! Otherwise there will be a conflict with 1.7.x versions of slf4j!
    compileOnly "org.slf4j:slf4j-api"

    compileOnly "org.projectlombok:lombok"
    testCompileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"
    testAnnotationProcessor "org.projectlombok:lombok"

    // ENI
    api "de.telekom:json-filter:${jsonFilterVersion}"

    api ("com.github.erosb:everit-json-schema:${everitJsonVersion}") {
        // fixes logging in test scope
        exclude group: 'commons-logging'
    }

    implementation 'org.springframework.boot:spring-boot'
    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.kafka:spring-kafka-test'

    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'

    testImplementation "de.bwaldvogel:mongo-java-server:1.45.0"

    // local dependencies
    testImplementation project(':horizon-spring-boot-autoconfigure')

    api "io.fabric8:kubernetes-client:${fabric8Version}"
    // Hazelcast
    api "com.hazelcast:hazelcast:${hazelcastVersion}"

    // Observability
    api 'io.micrometer:micrometer-tracing-bridge-brave'
    api 'io.zipkin.reporter2:zipkin-reporter-brave'
    api 'io.micrometer:micrometer-registry-prometheus'

    api "net.logstash.logback:logstash-logback-encoder:${logstashLogbackEncoderVersion}"

    api "org.apache.commons:commons-lang3"
}

publishing {
    publications {

        mavenJava(MavenPublication) {

            from components.java
            groupId 'de.telekom.eni'
            artifactId 'horizon-core'
            version = project.properties['version']

            pom {
                name = 'Horizon spring parent core'
                description = 'Spring-boot-starter core library for pubsub horizon java components.'
                url = 'https://github.com/telekom/pubsub-horizon-spring-parent'
                inceptionYear = '2022'

                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://opensource.org/licenses/Apache-2.0'
                    }
                }
                developers {
                    developer {
                        id = 'eni-pandora'
                        name = 'Team Pandora'
                        email = 'dtit_eni_hub_team_pandora@telekom.de'
                        organization = 'Deutsche Telekom IT GmbH'
                        organizationUrl = 'https://developer.telekom.de/'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/telekom/pubsub-horizon-spring-parent.git'
                    developerConnection = 'scm:git:git://github.com/telekom/pubsub-horizon-spring-parent.git'
                    url = 'https://github.com/telekom/pubsub-horizon-spring-parent'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = isReleaseVersion ? "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                    : "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            credentials {
                username = System.getenv('PUBLISH_USERNAME')
                password = System.getenv('PUBLISH_PASSWORD')
            }
        }
    }
}


test {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

test {
    useJUnitPlatform()
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }

    def signingKey = new String(Base64.decoder.decode(System.getenv('PUBLISH_GPG_PRIVATE_KEY') ?: ""))
    def signingPassword = System.getenv('PUBLISH_GPG_PASSPHRASE')
    useInMemoryPgpKeys(signingKey, signingPassword)

    sign publishing.publications.mavenJava
}

task("printVersion") {
    doLast {
        println "Version: ${project.version}"
    }
}

publish {
    finalizedBy printVersion
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}